plugins {
    id "com.modrinth.minotaur" version "2.+"
    id "fabric-loom" version "1.0-SNAPSHOT"
    id "maven-publish"
}

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
}

loom {
    accessWidenerPath = file("src/main/resources/${archivesBaseName}.accesswidener")
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 17
}

jar {
    from("LICENSE")
}

remapJar {
    classifier("mc${project.minecraft_version}")
}

if (System.getenv("MODRINTH_TOKEN") != null) {
    modrinth {
        token = System.getenv("MODRINTH_TOKEN")
        projectId = 'HcQm24Te'
        versionNumber = version
        uploadFile = remapJar
        gameVersions = [project.minecraft_version]
        loaders = ["fabric"]
        System.out.println("Enter changelog:")
        changelog = new BufferedReader(new InputStreamReader(System.in)).readLine()
    }
}
