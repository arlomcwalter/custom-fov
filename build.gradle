plugins {
    id "com.modrinth.minotaur" version "2.+"
    id "fabric-loom" version "1.1-SNAPSHOT"
}

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

configurations {
    tasks.modrinth.dependsOn(tasks.modrinthSyncBody)
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
}

loom {
    File aw = file("src/main/resources/${archivesBaseName}.accesswidener")
    if (aw.exists()) {
        accessWidenerPath = aw
    }
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release.set(17)
}

jar {
    from("LICENSE")
}

remapJar {
    classifier("mc${project.minecraft_version}")
}

if (System.getenv("MODRINTH_TOKEN") != null) {
    modrinth {
        token = System.getenv("MODRINTH_TOKEN")
        projectId = archivesBaseName
        versionNumber = version
        uploadFile = remapJar
        loaders = [ "fabric", "quilt" ]
        syncBodyFrom = rootProject.file("README.md").text
        gameVersions = [ project.minecraft_version ]
        System.out.println("Enter changelog:")
        changelog = new BufferedReader(new InputStreamReader(System.in)).readLine()
    }
}
